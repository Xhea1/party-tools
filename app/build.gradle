plugins {
    id 'application'
    id 'org.graalvm.buildtools.native' version '0.11.1'
    id "com.google.osdetector" version "1.7.3"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':library')

    // https://mvnrepository.com/artifact/com.google.guava/guava
    implementation 'com.google.guava:guava:33.4.8-jre'

    implementation 'org.apache.logging.log4j:log4j-core:2.25.1'

    implementation 'info.picocli:picocli:4.7.7'
    annotationProcessor 'info.picocli:picocli-codegen:4.7.7'

    implementation 'me.tongfei:progressbar:0.10.1'
}

application {
    // Define the main class for the application.
    mainClass = 'com.github.xhea1.party.app.Party'
}

graalvmNative {
    // TODO: disabled, because the native-image tool provisioned with the JVM is not executable
    toolchainDetection = false
    binaries {
        configureEach {
            imageName = "party-$osdetector.classifier-$rootProject.version"
            buildArgs.add("--strict-image-heap")
            buildArgs.add("-march=native")
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
        vendor = JvmVendorSpec.GRAAL_VM
        // TODO: re-enable once i figure out how to use it
        //nativeImageCapable = true
    }
}

tasks.named('assemble').configure {
    dependsOn 'nativeCompile'
}